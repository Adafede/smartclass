#+ summary: Which are the structures with InChI but no canonical SMILES?
#+ description: Which are the structures with InChI but no canonical SMILES?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Maintenance

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX hint: <http://www.bigdata.com/queryHints#>
#title: Which are the structures with InChI but no canonical SMILES?
SELECT ?structure ?inchi WHERE {
    VALUES ?classes {
        wd:Q113145171
        wd:Q59199015
    }
    ?structure wdt:P234 ?inchi. hint:Prior hint:rangeSafe true.
    ?structure wdt:P31 ?classes. hint:Prior hint:runFirst true.
    OPTIONAL { ?structure wdt:P233 ?smiles. }
    FILTER(!(BOUND(?smiles)))
}
