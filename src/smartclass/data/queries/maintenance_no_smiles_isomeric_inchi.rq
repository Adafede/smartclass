#+ summary: Which structures should have an isomeric SMILES but do not?
#+ description: Which structures should have an isomeric SMILES but do not?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Maintenance

PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX hint: <http://www.bigdata.com/queryHints#>
#title: Which structures should have an isomeric SMILES but do not?
SELECT ?structure ?inchi WHERE {
    ?structure wdt:P234 ?inchi. hint:Prior hint:rangeSafe true.
    OPTIONAL { ?structure wdt:P2017 ?smiles_i. }
    FILTER(!(BOUND(?smiles_i)))
    FILTER(REGEX(STR(?inchi), "/[btsmi]"))
}
