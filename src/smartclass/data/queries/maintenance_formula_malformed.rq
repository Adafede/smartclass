#+ summary: Chemicals with malformed chemical formula
#+ description: Chemicals with malformed chemical formula
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Chemicals
#+   - Chemical formula
#+   - Constraint

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
#title: Chemicals with malformed chemical formula
SELECT DISTINCT ?structure ?formula ?smiles WHERE {
    VALUES ?classes {
        wd:Q113145171
        wd:Q59199015
    }
    ?structure wdt:P274 ?formula.
    ?structure wdt:P233 ?smiles.
    ?structure wdt:P31 ?classes.
    wd:P274 wdt:P1793 ?constraint.
    FILTER(REPLACE(?formula, ?constraint, "") != "")
}
